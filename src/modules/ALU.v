// ALU.v

// This module performs ALU operations according to the "alu_func" value,
// which is generated by the ALU control unit.
// Note that there exist 10 R-type instructions in RV32I:
// add, sub, xor, or, and, sll, srl, sra, slt, sltu

`include "defines.v"

module ALU 
#(parameter DATA_WIDTH = 32)(
  input [DATA_WIDTH-1:0] in_a, 
  input [DATA_WIDTH-1:0] in_b,
  input [3:0] alu_func,

  output reg [DATA_WIDTH-1:0] result,
  output reg check 
);

// combinational logic 
always @(*) begin
  case (alu_func)
    `OP_ADD:  result = in_a +  in_b; 
    `OP_SUB:  result = in_a -  in_b;
    `OP_XOR:  result = in_a ^  in_b;
    `OP_OR:   result = in_a |  in_b;
    `OP_AND:  result = in_a &  in_b;

    //////////////////////////////////////////////////////////////////////////
    // TODO : Add other operations
    `OP_SLL : result = in_a << in_b[4:0];
    `OP_SRL : result = in_a >> in_b[4:0];
    `OP_SRA : result = $signed(in_a) >>> in_b[4:0];
    `OP_SLT : result = ($signed(in_a) < $signed(in_b)) ? 1 : 0;
    `OP_SLTU: result = (in_a < in_b) ? 1 : 0;
    // only for branch
    `OP_BGE : result = ($signed(in_a) >= $signed(in_b)) ? 1 : 0;
    `OP_BGEU: result = (in_a >= in_b) ? 1 : 0;
    //////////////////////////////////////////////////////////////////////////
    default:  result = 32'h0000_0000;
  endcase
end

// combinational logic
always @(*) begin
  case (alu_func)
    //////////////////////////////////////////////////////////////////////////
    // TODO : Generate check signal
    // check result is 0 or not / result == 0, check = 1
    `OP_ADD:   check = (1'b1);   // for jalr, always true
    `OP_XOR:   check = (result == 32'b0);   // beq: equal
    `OP_SUB:   check = (result != 32'b0);   // bne: not equal
    `OP_SLT:   check = (result == 32'b1);   // blt: signed less than
    `OP_SLTU:  check = (result == 32'b1);   // bltu: unsigned less than
    `OP_BGE:   check = (result == 32'b1);   // bge: not less than (greater or equal)
    `OP_BGEU:  check = (result == 32'b1);   // bgeu: not less than unsigned
    //////////////////////////////////////////////////////////////////////////
    default:  check = 1'b0;
  endcase
end
endmodule
